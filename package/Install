; EAB WHDLoad Demos AGA Menu Package Install Script
; -------------------------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2017-12-20
;
; This script extracts and installs zip archives for EAB WHDLoad Demos AGA Menu Package.


; Copy readme and screenshots to locale help for EAB WHDLoad Demos AGA Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale/Help.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale
  ENDIF
ENDIF
copy >NIL: PACKAGEDIR:README SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
copy >NIL: PACKAGEDIR:README.guide SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
copy >NIL: PACKAGEDIR:README.info SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu/screenshots
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu/screenshots
ENDIF
copy >NIL: PACKAGEDIR:screenshots/#?.iff SYSTEMDIR:Locale/Help/EAB-WHDLoad-Demos-AGA-Menu/screenshots


; Create systemdir demos directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Demos
  MakePath >NIL: SYSTEMDIR:Demos
ENDIF

echo "Installing Frontends..."
unzip -qq -o -x PACKAGEDIR:frontends.zip -d SYSTEMDIR:Demos/ 


echo "Installing Data Drawers..."
unzip -qq -o -x PACKAGEDIR:data-drawers.zip -d WHDLOADDIR:

echo "Configuring WHDLoad.prefs..."
IF NOT EXISTS SYSTEMDIR:S
  MakePath >NIL: SYSTEMDIR:S
ENDIF
IF NOT EXISTS "S:WHDLoad.prefs"
  Copy >NIL: PACKAGEDIR:WHDLoad.prefs SYSTEMDIR:S
ENDIF

; Disable existing splash deplay
rep "S:WHDLoad.prefs" "SplashDelay" ";SplashDelay"

; Set splash deplay in WHDLoad.prefs
echo "SplashDelay=50" >>S:WHDLoad.prefs 


echo "Installing Base Menus..."
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Demos"
  MakePath "WHDLOADDIR:Menu/AGS2Demos" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:ags2.zip -d WHDLOADDIR:Menu/AGS2Demos/
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Demos/Music"
  MakePath "WHDLOADDIR:Menu/AGS2Demos/Music" >NIL:
ENDIF
IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Demos/.Favourites.ags"
  IF NOT EXISTS "WHDLOADDIR:Menu/AGS2Demos/.Favourites.ag_"
    MakePath >NIL: "WHDLOADDIR:Menu/AGS2Demos/.Favourites.ag_"
  ENDIF
ENDIF

IF NOT EXISTS "WHDLOADDIR:Menu/HSTDemos"
  MakePath "WHDLOADDIR:Menu/HSTDemos" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d WHDLOADDIR:Menu/HSTDemos/


echo "Installing AGS2 Demos Menu..."
unzip -qq -o -x PACKAGEDIR:ags2-demos.zip -d WHDLOADDIR:Menu/AGS2Demos/
echo "Installing HST Demos Menu..."
unzip -qq -o -x PACKAGEDIR:hst-demos.zip -d WHDLOADDIR:Menu/HSTDemos/
echo "Installing Demos Frontends..."
unzip -qq -o -x PACKAGEDIR:frontends-demos.zip -d SYSTEMDIR:Demos/
echo "Installing Demos Data..."
unzip -qq -o -x PACKAGEDIR:data-demos.zip -d WHDLOADDIR:WHDLoad/Demos/ 


echo "Installing System Files..."
unzip -qq -o -x PACKAGEDIR:system-files.zip -d SYSTEMDIR:


; Add icon for configuration drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs/Configuration.info
  rename >NIL: SYSTEMDIR:Programs/Configuration.info.new SYSTEMDIR:Programs/Configuration.info
ELSE
  delete >NIL: SYSTEMDIR:Programs/Configuration.info.new
ENDIF

; Create default folder icon for systemdir demos and programs
IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
  IF NOT EXISTS SYSTEMDIR:Demos.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Demos.info
  ENDIF
  IF NOT EXISTS SYSTEMDIR:Programs.info
    copy SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info SYSTEMDIR:Programs.info
  ENDIF
ELSE
  IF EXISTS SYSTEMDIR:System.info
    IF NOT EXISTS SYSTEMDIR:Demos.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Demos.info
    ENDIF
    IF NOT EXISTS SYSTEMDIR:Programs.info
      copy SYSTEMDIR:System.info SYSTEMDIR:Programs.info
    ENDIF
  ENDIF
ENDIF


; Create systemdir whdload prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad
  MakePath >NIL: SYSTEMDIR:Prefs/WHDLoad
ENDIF

; Create default whdloadargs, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad/whdloadargs
  echo "Preload" > SYSTEMDIR:Prefs/WHDLoad/whdloadargs
ENDIF

; Create systemdir ags2 prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2
  MakePath >NIL: SYSTEMDIR:Prefs/AGS2
ENDIF

; Create ags2music prefs to turn on music in ags2 menu
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2/ags2music
  echo "" NOLINE > SYSTEMDIR:Prefs/AGS2/ags2music
ENDIF

; Create systemdir hst launcher prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/HSTLauncher
  MakePath >NIL: SYSTEMDIR:Prefs/HSTLauncher
ENDIF

; Create systemdir igame prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/iGame
  MakePath >NIL: SYSTEMDIR:Prefs/iGame
ENDIF


; Configure assigns
; -----------------

echo "Configuring Assigns..."

; Create assign startup, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Assign-Startup
	echo "" NOLINE >SYSTEMDIR:S/Assign-Startup
ENDIF

; Add execute assign startup to startup sequence, if it not ClassicWB and doesn't contain execute assign startup
search SYSTEMDIR:S/Startup-Sequence "ClassicWB" >NIL:
IF WARN
  search SYSTEMDIR:S/Startup-Sequence "execute S:Assign-Startup" >NIL:
  IF WARN
    rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "IF EXISTS S:Assign-Startup*N  execute S:Assign-Startup*NENDIF*N*NBindDrivers"
  ENDIF
ENDIF

; Add or update A-Demos to user-assign
set demosdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "WHDLoad/Demos"`"
search SYSTEMDIR:S/Assign-Startup "A-Demos:" >NIL:
IF WARN  
	echo "Assign >NIL: A-Demos: $demosdir" >>SYSTEMDIR:S/Assign-Startup
ELSE
  setenv updateassign `RequestChoice "Update assign" "A-Demos: assign already exists and must be updated to*Npath '$demosdir'.*N*NNote: Updating assign requires a reboot after*Ninstallation is done!*N*NDo you want to update A-Demos: assign?" "Yes|No"`
  IF "$updateassign" EQ "1"
    echo "$demosdir" >T:_demosdir
    set demosdir "`sed "s/\//\\\//g" T:_demosdir`"
    sed "s/\([Aa]-[Dd][Ee][Mm][Oo][Ss]:\)/\1 $demosdir;/" "SYSTEMDIR:S/Assign-Startup" >"T:Assign-Startup"
    copy >NIL: "T:Assign-Startup" "SYSTEMDIR:S/Assign-Startup"
  ENDIF   
ENDIF


; Configure menus
; ---------------
LAB configuremenus

echo "Configuring Menus..."

; Add or update AGS2Menus.cfg
set ags2demosdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "Menu/AGS2Demos"`"
search SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Demos=" >NIL:
IF WARN
  echo "AGS2Demos=$ags2demosdir" >>SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg
ELSE
  rep SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Demos=" "AGS2Demos=$ags2demosdir*N; "
ENDIF

; Add or update iGameMenus.cfg
set igamedemosdir "`execute INSTALLDIR:S/CombinePath "$SYSTEMDIR" "Demos"`"
search SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameDemos=" >NIL:
IF WARN
  echo "iGameDemos=$igamedemosdir" >>SYSTEMDIR:Prefs/iGame/iGameMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameDemos=" "iGameDemos=$igamedemosdir*N; "
ENDIF

; Add or update HSTLauncherMenus.cfg
set hstdemosdir "`execute INSTALLDIR:S/CombinePath "$WHDLOADDIR" "Menu/HSTDemos"`"
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTDemos=" >NIL:
IF WARN
  echo "HSTDemos=$hstdemosdir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTDemos=" "HSTDemos=$hstdemosdir*N; "
ENDIF

; End
LAB end